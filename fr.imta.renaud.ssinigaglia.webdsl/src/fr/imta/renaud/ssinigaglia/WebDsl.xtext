grammar fr.imta.renaud.ssinigaglia.WebDsl with org.eclipse.xtext.common.Terminals

generate webDsl "http://www.imta.fr/renaud/ssinigaglia/WebDsl"

Program:
	procedures += Procedure*
	cores+=Core*
;

// A test class
Core:
	'test' name=ID '{'
		'open_browser' browser=Browser
		actions += Action*
	'}'
;

Procedure:
	'PROCEDURE' name=ID'('var=Var'){'
		actions+=Action*
	'}'
;
	
enum Browser:
	CHROME = "chrome" 
	| FIREFOX = "firefox" 
	| SAFARI = "safari" 
	| EDGE = "edge" 
	| IE = "ie";

// Possible actions : Go to an url, select an html element, call a stored procedure
Action:
	GoAction
	| Selection 
	| CallProcedure
;

CallProcedure:
	'PROC:'ref=[Procedure]'('arg=STRING')'
;

GoAction:
	'go' url=STRING;
	
	
Selection:
	LinkButtonSelection 
	| GeneralSelection
	| CheckboxSelection 
	| ComboboxSelection
	| PageSelection
;

LinkButtonSelection:
	typeElement=("LINK"|"BUTTON"|"IMAGE") typeSelection=TypeSelection '{'
		(generalActions+=GeneralAction | clicks+='click')*
	'}'
;

GeneralSelection:
	typeElement=("SEARCH_FIELD"|"DIV") typeSelection=TypeSelection '{'
		(generalActions+=GeneralAction)*
	'}'
;

CheckboxSelection:
	typeElement="CHECKBOX" typeSelection=TypeSelection '{'
		action=('check'|'uncheck')
	'}'
;

ComboboxSelection:
	typeElement="COMBO_BOX" typeSelection=TypeSelection '{'
			('select' values+=STRING)*
	'}'
;

PageSelection :
	typeElement="PAGE" '{'
		(
			storeActions += StoreAction
			|assertions += Assert
		)*
	'}'
;

GeneralAction:
	SetAction
	| StoreAction
	| Selection
;

StoreAction:
	'store' '('(val=Attribute|count=CountAction)')' 'in' var=Var //TODO
;

CountAction:
	'count' htmlElement = HtmlElement typeSelection=TypeSelection
;

Var:
	name=ID
;
//

SetAction:
	'set' attribute=Attribute':'value=STRING 
;

TypeSelection:
	(attribute=Attribute':'(value=(STRING)|var=[Var]))
		|"PARENT" 
		|"FIRST" 
		|"LAST" 
		|"ALL"
;

Assert:
	"verify"
	(
		AssertContains
		|AssertEquals
	)
;

AssertContains : 'contains' ((htmlElement=HtmlElement attribute=Attribute) | text='TEXT') ':' value=STRING;
AssertEquals : c1=Comparable 'equals' c2=Comparable;

Comparable:
	'VAR:'var=Var
	|'('htmlElement = HtmlElement attribute=Attribute')'
	|STRING
;
enum HtmlElement:
	LINK = "LINK"
	|BUTTON = "BUTTON"
	|CHECKBOX = "CHECKBOX"
	|COMBOBOX = "COMBO_BOX"
	|DIV = "DIV"
	|IMAGE = "IMAGE"
	|A = "A"
	
;
enum Attribute:
	LABEL = "LABEL" 
	| ID = "ID" 
	| CLASS = "CLASS" 
	| ALT = "ALT" 
	| VALUE = "VALUE" 
	| HREF = "HREF" 
	| TITLE = "TITLE"
	| NAME = "NAME"
;

